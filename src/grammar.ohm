AsciiDoc {
  Document = DocElement* EndOfFile
  DocElement =
    | DocumentAttribute -- attr
    | Block -- block

  wrapping_attr_text = (~newline ~space_backslash any)+ space_backslash
  DocumentAttributeValueWrappedText = wrapping_attr_text newline DocumentAttributeValue
  DocumentAttributeValueSingleText = (~newline any)* newline
  DocumentAttributeValue =
    | DocumentAttributeValueWrappedText -- val_wrapped
    | DocumentAttributeValueSingleText -- val
  document_attribute_key = (~":" ~space any)+
  DocumentAttribute = ":" document_attribute_key ":" DocumentAttributeValue

  AttributeList = "[" listOf<Attribute, ","> "]"
  Attribute = AttributeNamed | AttributePositional
  AttributePositional = (~"]" ~"," any)+
  AttributeNamed = AttributePositional "=" AttributePositional

  BlockStructure<t> = BlockAnchor? BlockMetaData? t newline? newline?
  Block =
    | BlockStructure<Fence> -- blockf
    | BlockStructure<NonFencable>  -- blocknf

  BlockAnchor = "[[" anchor_id anchor_reftext?  "]]" newline
  anchor_id = (~"]" ~"," any)+
  anchor_reftext = "," (~"]]" any)*
  BlockMetaData = block_title? AttributeList newline
  block_title = "." ~space (~newline any)* newline

  NonFencable =
    | Header
    | BlockSectionSetext
    | BlockAdmonition
    | BlockQuotedParagraph
    | BlockImage
    | BlockList
    | BlockHorizontalRule
    | BlockMacro
    | BlankLine
    | BlockParagraph
    | newline

  BlockContentGeneric<delim> = newline? (~delim InlineElementOrText)+ BlockContentGeneric<delim>?

  Fencable<delim> =
  | BlockAdmonition
  | BlockTable
  | BlockContentGeneric<delim>
  // | BlockQuote -- AST only
  // | BlockComment -- AST only
  // | BlockSidebar -- AST only
  // | BlockPassthrough -- AST only

  doublequote = "\""

  BlockQuotedParagraph = doublequote (~doublequote any)* doublequote newline "--" any_non_newline? newline

  LineElementStart = "="

  inlineelstart =
      | "*"  // constrained bold
      | "**"  // unconstrained Bold
      | "_"  // constrained Italics
      | "__"  // Unconstrained Italics
      | "//"  // Italic
      | "`"   // Monospace
      | "<<"  // Cross-reference start
      | "link:"  // Link start
      | "image:"  // Image start
      | "footnote:"  // Footnote start
      | "+++"  // Inline passthrough start
      | "~"   // Subscript start
      | "^"   // Superscript start
      | "{"   // Attribute reference start

  EndOfFile = newline? end  // Allow optional newline at end of file
  SingleLineText = InlineElement+ newline?

  BlockSectionSetext = HeaderTextSetext HeaderUnderlineSetext BlankLine*
  HeaderTextSetext = line
  HeaderUnderlineSetext = (("=" | "-")+ newline) | (("=" | "-")+ end)


  Header = HeaderMarker HeaderContent newline?
  HeaderMarker =
  | "======"
  | "====="
  | "===="
  | "==="
  | "=="
  | "="
  HeaderContent = (~newline InlineElementOrText)+

  BlockParagraph = (~LineElementStart) ParagraphSegment
  ParagraphSegment = InlineElementOrText+ (BlankLine ParagraphSegment)?

  BlockList = UnorderedList
       | OrderedList
       | DescriptionList

  UnorderedList = UnorderedListItem+
  UnorderedListItem = unordered_list_marker ListItemContent newline?
  ListItemContent = InlineElementOrText+ (ListContinuation ListItemContent+)*
  unordered_list_marker = ("*" | "-" | "â€¢")+ space+

  OrderedList = OrderedListItem+
  OrderedListItem = OLMarker ListItemContent newline?
  ol_marker_dot = ". "
  OLMarkerDigits = Digits ol_marker_dot
  OLMarker = OLMarkerDigits | ol_marker_dot

  DescriptionList = DescriptionListItem+
  DescriptionListItem = Term "::" DescriptionContent

  ListContinuation = "+" newline ListItemContent

  Term = (~"::" any)+

  line = (~newline any)+ newline

  DescriptionContent = line+

  BlockTable = TableRow+
  TableRow = "|" TableCell ("|" TableCell)* newline
  TableCell = InlineElement+

  BlockHorizontalRule = "'''" newline

  BlockAdmonition = AdmonitionType any_non_newline+
  AdmonitionType = "NOTE:" | "TIP:" | "IMPORTANT:" | "WARNING:" | "CAUTION:"


  PassthroughDelimiter = "++++"

  BlockMacro = MacroName "::" MacroTarget? AttributeList? newline
               (line | BlankLine)*
               MacroDelimiter newline
  MacroName = letter+
  MacroTarget = (~"[" any)+

  MacroDelimiter = "----"

  AttributeEntry = ":" AttributeName ":" AttributeValue? newline
  AttributeName = (~":" any)+
  AttributeValue = (~InlineElement any)*

  comment_delimiter = "////"

  InlineElement =
    | AttributeReference
    | ConstrainedBold
    | ConstrainedItalic
    | CrossReference
    | Footnote
    | InlineImage
    | InlinePassthrough
    | Link
    | MonospaceText
    | SubscriptText
    | SuperscriptText
    | UnconstrainedBold
    | UnconstrainedItalic
    | UrlMacro

  InlineElementOrText = InlineElement | any_non_newline

  any_non_newline = (~newline any)+
  // plaintext = (~inlineelstart ~newline ~httpscheme any)+

  ConstrainedBold = "*" (~"*" InlineElementOrText)+ "*"
  UnconstrainedBold = "*" ConstrainedBold "*"

  UnconstrainedItalic = "_" (~"_" InlineElementOrText)+ "_"
  ConstrainedItalic = "_" UnconstrainedItalic "_"

  MonospaceText = "`" (~"`" any)+ "`"
  httpscheme = "https" | "http"
  UrlMacroScheme = httpscheme | "ftp" | "irc" | "mailto"
  UrlMacro = UrlMacroScheme ":" (~"[" any)+ AttributeList

  Link = "link:" Url "[" LinkText "]"
  Url = (~"[" any)+
  LinkText = (~"]" any)*

  ImageUrl = (~"[" any)+
  ImageH = Digits
  ImageW = Digits
  ImageDims = ImageW ","? ImageH?
  InlineImage = "image:" ImageUrl "[" ImageAlt ("," ImageDims)? "]"
  BlockImage = "image::" ImageUrl "[" ImageAlt ("," ImageDims)? "]"
  ImageAlt = (~"]" ~"," any)*

  Footnote = "footnote:" FootnoteId? "[" FootnoteText "]"
  FootnoteId = Digits
  FootnoteText = (~"]" any)*

  CrossReference = "<<" CrossReferenceId CrossReferenceText? ">>"
  CrossReferenceId = (~"," ~">>" any)+
  CrossReferenceText = "," (~">>" any)*

  InlinePassthrough = "+++" (~"+++" any)+ "+++"

  SubscriptText = "~" (~"~" any)+ "~"
  SuperscriptText = "^" (~"^" any)+ "^"

  AttributeReference = "{" AttributeName "}"

  BlankLine = space* newline

  Digits = digit+

  backslash = "\\"
  space_backslash = " " backslash
  newline = "\n" | "\r" | "\r\n"
  space := " " --space
    | "\t" --tab

  FenceGeneric<delim, content> = delim newline content newline delim

  table_fence_variant_1_pipe = "|==="
  table_fence_variant_2_comma = ",==="
  table_fence_variant_3_colon = ":==="
  table_fence_variant_4_bang = "!==="
  FenceTable =
    | FenceGeneric<table_fence_variant_1_pipe, Fencable<table_fence_variant_1_pipe>>
    | FenceGeneric<table_fence_variant_2_comma, Fencable<table_fence_variant_2_comma>>
    | FenceGeneric<table_fence_variant_3_colon, Fencable<table_fence_variant_3_colon>>
    | FenceGeneric<table_fence_variant_4_bang, Fencable<table_fence_variant_4_bang>>


  FenceOpen = FenceGeneric<"--", Fencable<"--">>

  FenceOfDelim4To7<delim> =
    | FenceGeneric<of_7<delim>, Fencable<of_7<delim>>> -- fenced7
    | FenceGeneric<of_6<delim>, Fencable<of_6<delim>>> -- fenced6
    | FenceGeneric<of_5<delim>, Fencable<of_5<delim>>> -- fenced5
    | FenceGeneric<of_4<delim>, Fencable<of_4<delim>>> -- fenced4

  FenceDot = FenceOfDelim4To7<".">
  FenceUnder = FenceOfDelim4To7<"_">
  FenceDash = FenceOfDelim4To7<"-">
  FenceEq = FenceOfDelim4To7<"=">
  FenceStar = FenceOfDelim4To7<"*">
  FenceSlash = FenceOfDelim4To7<"/">
  FencePlus = FenceOfDelim4To7<"+">
  Fence =
    | FenceUnder -- Under
    | FenceDot -- Dot
    | FenceDash -- Dash
    | FenceEq -- Eq
    | FenceStar -- Star
    | FenceSlash -- Slash
    | FencePlus -- Plus
    | FenceOpen -- Open
    | FenceTable -- Table

  of_2<x> = x x
  of_3<x> = x of_2<x>
  of_4<x> = x of_3<x>
  of_5<x> = x of_4<x>
  of_6<x> = x of_5<x>
  of_7<x> = x of_6<x>
  of_8<x> = x of_7<x>
}
